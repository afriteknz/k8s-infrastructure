locals {
  infra_rg_name       = "k8s-rg"
  infra_nodes_rg_name = "k8s-nodes-rg"
}


# Create resource group name
resource "azurerm_resource_group" "rg" {
  location = var.resource_group_location
  name     = local.infra_rg_name
}


# create acr Container registery
resource "azurerm_container_registry" "acr" {
  name                = "cubemcr"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  sku                 = "Standard"
  admin_enabled       = true
}

resource "azurerm_kubernetes_cluster" "k8s" {
  location            = azurerm_resource_group.rg.location
  name                = var.kubernetes_cluster_name
  resource_group_name = azurerm_resource_group.rg.name
  dns_prefix          = var.kubernetes_cluster_name
  node_resource_group = local.infra_nodes_rg_name


  identity {
    type = "SystemAssigned"
  }

  default_node_pool {
    name                        = "agentpool"
    vm_size                     = "Standard_B2s"
    os_disk_size_gb             = 30
    node_count                  = var.node_count
    temporary_name_for_rotation = "tmpdefault"
  }
  linux_profile {
    admin_username = var.username

    ssh_key {
      # key_data = jsondecode(azapi_resource_action.ssh_public_key_gen.output).publicKey
      key_data = azapi_resource_action.ssh_public_key_gen.output.publicKey
    }
  }
  network_profile {
    network_plugin    = "kubenet"
    load_balancer_sku = "standard"
  }

  # azure_active_directory_role_based_access_control {
  #   managed            = true
  #   azure_rbac_enabled = true
  # }

  tags = {
    Environment = "Dev"
  }
}

# The AKS nodes resource group (auto generated by AKS cluster)
data "azurerm_resource_group" "nodes" {
  name       = azurerm_kubernetes_cluster.k8s.node_resource_group
  depends_on = [azurerm_kubernetes_cluster.k8s]
}


# create role assignment for aks acr pull
resource "azurerm_role_assignment" "example" {
  principal_id                     = azurerm_kubernetes_cluster.k8s.kubelet_identity[0].object_id
  role_definition_name             = "AcrPull"
  scope                            = azurerm_container_registry.acr.id
  skip_service_principal_aad_check = true
}
